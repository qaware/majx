buildscript {
    ext.kotlin_version = '1.1.3-2'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version '1.1.3-2'
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.6.3'
    id "com.github.hierynomus.license" version "0.14.0"
    id 'org.jetbrains.dokka' version '0.9.15'
    id 'net.researchgate.release' version '2.6.0'
}

group 'de.qaware'

repositories {
    mavenCentral()
}

ext.versions = [
    jacksonVersion     : '2.8.9',
    hamcrestVersion    : '1.3',
    commonsIoVersion   : '2.5',
    commonsLang3Version: '3.6',
    slf4JVersion       : '1.7.21',
    guavaVersion       : '22.0'
]

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    testCompile "org.jetbrains.kotlin:kotlin-test"

    // JSON Mapping
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jacksonVersion
    // https://mvnrepository.com/artifact/com.fasterxml.jackson.datatype/jackson-datatype-joda
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: versions.jacksonVersion

    // Assertions
    // https://mvnrepository.com/artifact/org.hamcrest/hamcrest-all
    compile group: 'org.hamcrest', name: 'hamcrest-all', version: versions.hamcrestVersion

    // Mustache
    compile 'com.github.spullara.mustache.java:compiler:0.9.5'

    // Utils
    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: versions.commonsIoVersion
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    compile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3Version
    // https://mvnrepository.com/artifact/com.google.guava/guava
    compile group: 'com.google.guava', name: 'guava', version: versions.guavaVersion

    // Logging
    // http://mvnrepository.com/artifact/org.slf4j/slf4j-api
    compile group: 'org.slf4j', name: 'slf4j-api', version: versions.slf4JVersion

    // https://mvnrepository.com/artifact/junit/junit
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dokka {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

coveralls {
    // Add kotlin source dir, since the coveralls plugin needs the sources and
    // does not pick it up automatically.
    // See https://github.com/kt3k/coveralls-gradle-plugin/issues/63
    sourceDirs += ['src/main/kotlin']
}

license {
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
    header file("LICENSE")
    strictCheck true
    include '**/*.kt'
    include '**/*.java'
}

release {
    tagCommitMessage = '[#1] Gradle Release Plugin - creating tag: '
    newVersionCommitMessage = '[#1] Gradle Release Plugin - new version: '
    tagTemplate = 'v${version}'
}

def pomConfig = {

    inceptionYear '2017'

    scm {
        connection "scm:git:${project.scmUrl}"
        developerConnection "scm:git:${project.scmUrl}"
        url project.websiteUrl
    }

    issueManagement {
        system 'GitHub'
        url project.issueTrackerUrl
    }

    licenses {
        license([:]) {
            name 'MIT License'
            url 'https://opensource.org/licenses/MIT'
            distribution 'repo'
        }
    }

    organisation {
        name 'QAware GmbH'
        url 'https://www.qaware.de'
    }

    developers {
        developer {
            id 'cboettcher'
            name 'Claudius Boettcher'
            email 'claudius.boettcher@qaware.de'
            organization 'QAware GmbH'
            organizationUrl 'https://www.qaware.de'
            roles { role 'Developer' }
        }
    }
}

publishing {
    publications {
        basePlugin(MavenPublication) {
            from components.java
            archives javadocJar
            archives sourcesJar

            pom.withXml {
                asNode().appendNode('name', project.displayName)
                asNode().appendNode('description', project.description)
                asNode().appendNode('url', project.websiteUrl)

                asNode().children().last() + pomConfig
            }
        }
    }
    repositories {
        // set the properties via -P to publish to your company repo
        maven {
            url = project.hasProperty('nexusUrl') ? project.nexusUrl : ''
            credentials {
                username = project.hasProperty('nexusUsername') ? project.nexusUsername : ''
                password = project.hasProperty('nexusPassword') ? project.nexusPassword : ''
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUsername') ? project.bintrayUsername : 'unknown'
    key = project.hasProperty('bintrayApiKey') ? project.bintrayApiKey : 'unknown'
    publications = ['basePlugin']
    dryRun = false
    publish = true
    pkg {
        repo = project.bintrayRepo
        name = project.name
        desc = project.description
        licenses = ['MIT']
        labels = ['JSON', 'validation', 'matching', 'test', 'kotlin']
        websiteUrl = project.websiteUrl
        issueTrackerUrl = project.issueTrackerUrl
        vcsUrl = project.scmUrl
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.description
            released = new Date().format("yyyy-MM-dd'T'HH:mm:ss.SSSZZ")
            vcsTag = "v${project.version}"
            mavenCentralSync {
                sync = false
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0.1'
}
